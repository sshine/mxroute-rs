# Git hooks via hk-nix. The generated hk.pkl is symlinked into the repo root by
# the devshell startup hook (see devshell.nix); hk.pkl is gitignored.
{ inputs, ... }:
{
  imports = [ inputs.hk-nix.flakeModules.default ];
  perSystem =
    {
      config,
      pkgs,
      lib,
      ...
    }:
    let
      # Flags to reproduce the committed README.md from README.tpl and the crate docs.
      readmeArgs = "--project-root crates/mxroute --input src/lib.rs --template ../../README.tpl";

      # Reference tools by absolute store path: the `nix flake check` hk-check sandbox
      # runs hooks without the devshell PATH, so a bare `treefmt` is not found there.
      treefmt = lib.getExe config.treefmt.build.wrapper;
      betterleaks = lib.getExe pkgs.betterleaks;

      # Called by store path rather than via `cargo readme`, so the cargo-subcommand
      # argv has to be supplied by hand: without it clap only prints its usage.
      cargo-readme = "${lib.getExe' pkgs.cargo-readme "cargo-readme"} readme";

      # cargo-readme emits markdown that mdformat then rewrites (link reference
      # definitions move to the end of the file), so the raw output never equals the
      # committed README.md and the two hooks would undo each other forever. Reuse
      # treefmt's own mdformat so the plugin set cannot drift from the pre-commit one.
      mdformat = config.treefmt.settings.formatter.mdformat.command;

      readme = "${cargo-readme} ${readmeArgs} | ${mdformat} -";
    in
    {
      hk-nix.settings.hooks = {
        "pre-commit" = {
          fix = true;
          stash = "git";
          steps = {
            treefmt = {
              check = "${treefmt} --fail-on-change --no-cache {{files}}";
              fix = "${treefmt} {{files}}";
            };

            # Here rather than pre-push: a secret caught before it is committed costs an
            # amend, one caught later costs a history rewrite and a key rotation. It only
            # ever sees the staged files, so it stays fast enough for this stage.
            #
            # An API key belongs in the developer's secretspec provider (see
            # secretspec.toml); nothing in this repository should ever hold one.
            #
            # Spelled out rather than using the hk builtin, which passes an explicit file
            # list: given one, betterleaks stops looking for .betterleaks.toml beside the
            # files, and the exemptions for the fake credentials in the tests go unread.
            betterleaks = {
              check = "${betterleaks} dir --redact --no-banner --config .betterleaks.toml {{files}}";
            };
          };
        };

        "pre-push".steps = {
          deadnix = {
            glob = "*.nix";
            check = "${lib.getExe pkgs.deadnix} --fail {{files}}";
          };
          clippy = {
            check = "cargo clippy --all-targets --all-features -- -D warnings";
          };
          readme = {
            check = "${readme} | diff - README.md";
            fix = "${readme} > README.md";
          };
          lock-check = {
            check = "cargo metadata --locked --format-version 1 > /dev/null";
          };
        };

        # hk runs this one itself, so no tool needs pinning. Note it has no
        # equivalent of --require-scope; --allowed-types is the only policy knob.
        "commit-msg".steps.conventional.builtin = config.hk-nix.builtins.check_conventional_commit;
      };
    };
}
